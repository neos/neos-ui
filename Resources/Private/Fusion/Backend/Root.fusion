include: resource://Neos.Fusion/Private/Fusion/Root.fusion
include: resource://Neos.Neos/Private/Fusion/Prototypes/NodeUri.fusion
include: resource://Neos.Neos.Ui/Private/Fusion/Prototypes/RenderState.fusion
include: resource://Neos.Neos.Ui/Private/Fusion/Prototypes/ArrayCollection.fusion
include: resource://Neos.Neos.Ui/Private/Fusion/Prototypes/AppendAllToCollection.fusion

backend = Neos.Fusion:Template {
    templatePath = 'resource://Neos.Neos.Ui/Private/Templates/Backend/Index.html'

    headScripts = ${headScripts}
    headStylesheets = ${headStylesheets}

    configuration = Neos.Fusion:RawArray {
        test = 1
        allowedTargetWorkspaces = ${Neos.Ui.Workspace.getAllowedTargetWorkspaces()}
        @process.json = ${Json.stringify(value)}
    }

    frontendConfiguration = Neos.Neos.Ui:RenderState {
        state = 'frontendConfiguration'
        @process.json = ${Json.stringify(value)}
    }

    nodeTypes = Neos.Fusion:RawArray {
        roles = ${Configuration.setting('Neos.Neos.Ui.nodeTypeRoles')}
        byName = ${Neos.Ui.NodeTypes.nodeTypesByName()}
        constraints = ${Neos.Ui.NodeTypes.nodeTypeConstraints()}
        inheritanceMap = ${Neos.Ui.NodeTypes.nodeTypeInheritanceMap()}
        groups = ${Configuration.setting('Neos.Neos.nodeTypes.groups')}

        @process.json = ${Json.stringify(value)}
    }

    menu = Neos.Fusion:RawArray {
        content = Neos.Fusion:RawArray {
            label = 'Neos.Neos:Main:content'
            icon = 'file'
            uri = Neos.Neos:NodeUri {
                node = ${q(site).parent().children().get(0)}
            }
            target = 'ContentCanvas'

            children = Neos.Neos.Ui:ArrayCollection {
                collection = ${q(site).parent().children().get()}
                itemName = 'siteNode'
                itemRenderer = Neos.Fusion:RawArray {
                    icon = 'globe'
                    label = ${q(siteNode).property('title')}
                    uri = Neos.Neos:NodeUri {
                        node = ${siteNode}
                    }
                    target = 'ContentCanvas'
                    isActive = ${Neos.Ui.Sites.isActive(siteNode)}
                    skipI18n = ${true}
                }
            }
        }

        @process.modules = Neos.Neos.Ui:AppendAllToCollection {
            appendTo = ${value}
            collection = ${Configuration.setting('Neos.Neos.modules')}
            itemName = 'module'
            itemKey = 'moduleName'

            itemKeyRenderer = ${moduleName}
            itemRenderer = Neos.Fusion:RawArray {
                label = ${module.label}
                icon = ${module.icon}
                uri = Neos.Fusion:UriBuilder {
                    package = 'Neos.Neos'
                    controller = 'Backend\\Module'
                    action = 'index'
                    absolute = true
                    arguments {
                        module = ${moduleName}
                    }
                }
                target = 'Window'

                children = Neos.Neos.Ui:ArrayCollection {
                    collection = ${module.submodules}
                    itemName = 'submodule'
                    itemKey = 'submoduleName'

                    itemRenderer = Neos.Fusion:RawArray {
                        icon = ${submodule.icon}
                        label = ${submodule.label}
                        uri = Neos.Fusion:UriBuilder {
                            package = 'Neos.Neos'
                            controller = 'Backend\\Module'
                            action = 'index'
                            absolute = true
                            arguments {
                                module = ${moduleName + '/' + submoduleName}
                            }
                        }
                        isActive = true
                        target = 'Window'
                    }
                }

                @if.isAvailable = ${Neos.Neos.Ui.Modules.isAvailable(moduleName)}
            }
        }

        @process.json = ${Json.stringify(value)}
    }

    initialState = Neos.Neos.Ui:RenderState {
        state = 'backend'
        context {
            documentNode = ${documentNode}
            site = ${site}
            user = ${user}
        }

        @process.json = ${Json.stringify(value)}
    }

    translations = ${translations}

    env = ${Configuration.setting('Neos.Flow.core.context')}
}
