//
// Reset some rendering steps of the Neos backend, since the package
// augments the website itself
//
prototype(Neos.Neos:Page) {

    //
    // Disable all html attributes printed for the Neos backend
    //
    htmlTag.attributes {
        version.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
        xmlns.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
        xmlns:typo3.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
        xmlns:xsd.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
    }

    //
    // Disable rendering of the Neos backend
    //
    head {
        neosBackendHeader.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
        neosBackendEndpoints.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}

        javascriptBackendInformation = Neos.Neos.Ui:RenderConfiguration {
            path = 'documentNodeInformation'
            context {
                documentNode = ${documentNode}
                site = ${site}
            }

            @position = 'after javascripts'

            @process.json = ${Json.stringify(value)}
            @process.wrapInJsObject = ${'<script>window[\'@Neos.Neos.Ui:DocumentInformation\']=' + value + '</script>'}
            @if {
                inBackend = ${documentNode.context.inBackend}
                newBackend = ${Neos.Ui.Activation.enableNewBackend()}
            }

            // We need to ensure the JS backend information is always up to date, especially
            // when child nodes change. Otherwise errors like the following might happen:
            // - create a new child (document) node
            // - again visit the parent node
            // - the parent node still has the stale "children" infos (without the newly created child)
            // - thus, the document tree will have the newly created node REMOVED again (visually).
            //
            // as a fix, we ensure that the JS backend information creates an own cache entry, which is flushed
            // whenever children are modified.
            @cache {
                mode = 'cached'
                entryIdentifier {
                    jsBackendInfo = 'javascriptBackendInformation'
                    documentNode = ${documentNode}
                    inBackend = ${documentNode.context.inBackend}
                }
                entryTags {
                    1 = ${'Node_' + documentNode.identifier}
                    2 = ${'DescendantOf_' + documentNode.identifier}
                }
            }
        }

        guestFrameApplication = Neos.Fusion:Template {
            @position = 'after javascriptDocumentInformation'

            templatePath = 'resource://Neos.Neos.Ui/Private/Templates/Backend/Guest.html'
            compiledResourcePackage = ${Neos.Ui.StaticResources.compiledResourcePackage()}

            sectionName = 'guestFrameApplication'
            @if {
                inBackend = ${documentNode.context.inBackend}
                newBackend = ${Neos.Ui.Activation.enableNewBackend()}
            }
        }
    }

    //
    // Do not print document meta data
    //
    neosBackendDocumentNodeData.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}

    //
    // Do not render the Neos backend container
    //
    neosBackendContainer.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}

    newNeosBackendWrappingElement = '<div id="neos-new-backend-container"></div>'
    newNeosBackendWrappingElement.@position = 'after neosBackendContainer'
    newNeosBackendWrappingElement.@if.newBackend = ${Neos.Ui.Activation.enableNewBackend()}

    //
    // Do not render the Neos backend footer
    //
    neosBackendFooter.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}

    neosBackendNotification = Neos.Fusion:Template {
        @position = 'before closingBodyTag'
        templatePath = 'resource://Neos.Neos.Ui/Private/Templates/Backend/GuestNotificationScript.html'
        @if {
            inBackend = ${documentNode.context.inBackend}
            newBackend = ${Neos.Ui.Activation.enableNewBackend()}
        }
    }

    //
    // Output `<script>` tags for all non rendered node children
    //
    neosUiNonRenderedNodeMetadata = Neos.Neos.Ui:RenderNonRenderedNodeMetadata {
        @position = 'before neosBackendNotification'
        @class = 'Neos\\Neos\\Ui\\Fusion\\RenderNonRenderedNodeMetadataImplementation'
        @if.newBackend = ${Neos.Ui.Activation.enableNewBackend()}
        node = ${node}
    }
}
