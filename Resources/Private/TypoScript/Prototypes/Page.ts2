//
// Reset some rendering steps of the Neos backend, since the package
// augments the website itself
//
prototype(Neos.Neos:Page) {

    //
    // Disable all html attributes printed for the Neos backend
    //
    htmlTag.attributes {
        version.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
        xmlns.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
        xmlns:typo3.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
        xmlns:xsd.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
    }

    //
    // Disable rendering of the Neos backend
    //
    head {
        neosBackendHeader.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
        neosBackendEndpoints.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}

        javascriptBackendInformation = Neos.Neos.Ui:RenderState {
            state = 'documentNode'
            context {
                documentNode = ${documentNode}
            }

            @position = 'after javascripts'

            @process.json = ${Json.stringify(value)}
            @process.wrapInJsObject = ${'<script>window[\'@Neos.Neos.Ui:DocumentInformation\']=' + value + '</script>'}
            @if.newBackend = ${Neos.Ui.Activation.enableNewBackend()}
        }

        guestFrameApplication = Neos.Fusion:Template {
            @position = 'after javascriptDocumentInformation'

            templatePath = 'resource://Neos.Neos.Ui/Private/Templates/Backend/Guest.html'
            compiledResourcePackage = ${Neos.Ui.StaticResources.compiledResourcePackage()}

            sectionName = 'guestFrameApplication'
            @if.newBackend = ${Neos.Ui.Activation.enableNewBackend()}
        }
    }

    //
    // Do not add Neos backend clas
    //
    bodyTag.attributes.class.@process.addNeosBackendClass.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}

    //
    // Do not print document meta data
    //
    neosBackendDocumentNodeData.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}

    //
    // Do not render the Neos backend container
    //
    neosBackendContainer.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}

    newNeosBackendWrappingElement = '<div id="neos-new-backend-container"></div>'
    newNeosBackendWrappingElement.@position = 'after neosBackendContainer'
    newNeosBackendWrappingElement.@if.newBackend = ${Neos.Ui.Activation.enableNewBackend()}

    //
    // Disable memorizing last visited node
    //
    lastVisitedNodeScript.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}

    //
    // Do not render the Neos backend footer
    //
    neosBackendFooter.@if.oldBackend = ${!Neos.Ui.Activation.enableNewBackend()}
}
